plugins {
    id("com.github.johnrengelman.shadow") version "7.1.2"
    id("io.micronaut.application") version "3.6.2"
}

group = "org.dandoy.dbpopd"
version dbpopVersion

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor("org.projectlombok:lombok")
    annotationProcessor("io.micronaut:micronaut-http-validation")
    annotationProcessor("io.micronaut.openapi:micronaut-openapi")
    implementation("io.micronaut:micronaut-http-client")
    implementation("io.micronaut:micronaut-jackson-databind")
    implementation("io.micronaut:micronaut-validation")
    implementation("io.micronaut.problem:micronaut-problem-json")
    implementation("io.swagger.core.v3:swagger-annotations")
    implementation("jakarta.annotation:jakarta.annotation-api")
    compileOnly("org.projectlombok:lombok")
    implementation 'ch.qos.logback:logback-classic:1.4.4'
    implementation('org.slf4j:jcl-over-slf4j:2.0.3')
    implementation project(":lib")
    implementation project(":diff")
    implementation 'org.apache.commons:commons-csv:1.9.0'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation "io.github.java-diff-utils:java-diff-utils:4.12"
    testImplementation project(":test-resources")
}

application {
    mainClass.set("org.dandoy.dbpopd.Application")
}
java {
    sourceCompatibility = JavaVersion.toVersion("17")
    targetCompatibility = JavaVersion.toVersion("17")
}

dockerBuild {
    inputDir = project.projectDir
    images = ["docker.io/cdandoy/dbpop:$project.version","docker.io/cdandoy/dbpop:latest"]
}

graalvmNative.toolchainDetection = false
micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("org.dandoy.dbpopd.*")
    }
}

task('copyClientResources', type: Copy) {
    from "${project(':dbpopr').buildDir}"
    into "${project(':dbpopd').buildDir}/resources/main/public"
}

task versionTxt()  {
    doLast {
        def file = new File(buildDir, "resources/main/version.txt")
        file.getParentFile().mkdirs()
        file.text = "app.version=$version\n"
    }
}

processResources.finalizedBy([copyClientResources, versionTxt])

